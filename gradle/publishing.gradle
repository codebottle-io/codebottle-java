apply plugin: 'maven-publish'
apply plugin: "io.codearte.nexus-staging"

publishing {
    repositories {
        maven {
            url = snapshot
                    ? "https://oss.sonatype.org/content/repositories/snapshots"
                    : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            name = "Sonatype"

            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = artifactName
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = projectName
                description = projectDescription
                inceptionYear = '2019'
                url = 'https://github.com/codebottle-io/codebottle-java'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = "burdoto"
                        name = "Tobias Burdow"
                        email = "burdoto@outlook.com"
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/codebottle-io/codebottle-java.git'
                    developerConnection = 'scm:git:ssh://github.com/codebottle-io/codebottle-java.git'
                    url = 'https://github.com/codebottle-io/codebottle-java'
                }
            }
        }
    }
}

task generateGitHubPom(type: GenerateMavenPom) {
    destination = "pom.xml"
    pom = publishing.publications.mavenJava.pom
}

if (!System.getenv().containsKey("JITPACK")) {
    apply plugin: 'signing'
    
    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}

nexusStaging {
    username sonatypeUsername
    password sonatypePassword
}
